plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.github.mwierzchowski'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.13'
targetCompatibility = '1.13'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Code processors
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.18'

	// Compilation
	compileOnly 'org.projectlombok:lombok'

	// Implementation
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.6.1'
	implementation 'org.mapstruct:mapstruct:1.3.1.Final'
	implementation 'io.swagger.core.v3:swagger-jaxrs2:2.1.1'
	implementation 'io.github.resilience4j:resilience4j-spring-boot2:0.16.0'
	implementation 'org.postgresql:postgresql'
	implementation 'org.liquibase:liquibase-core'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
	testImplementation 'org.springframework.cloud:spring-cloud-starter:2.2.2.RELEASE'
	testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:2.2.2.RELEASE'
	testImplementation 'com.playtika.testcontainers:embedded-postgresql:1.43'
	testImplementation 'org.apache.commons:commons-lang3:3.10'

	// Dev tools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

def owmClientPackage = "org.openweathermap"
swaggerSources {
	owm {
		inputFile = file('src/main/openapi/openapi-owm.yaml')
		code {
			language = 'java'
			library = "resttemplate"
			outputDir = file("$buildDir/generated/sources/owmSwagger")
			additionalProperties = [
					dateLibrary : "java8",
					apiPackage : "${owmClientPackage}.api",
					modelPackage : "${owmClientPackage}.model",
					invokerPackage : "${owmClientPackage}.invoker"
			]
		}
	}
}
compileJava.dependsOn swaggerSources.owm.code
sourceSets.main.java.srcDirs += "${swaggerSources.owm.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDirs += "${swaggerSources.owm.code.outputDir}/src/main/resources"

// todo maybe this can be removed as build is not silent anyway
test {
	// Workaround for https://github.com/gradle/gradle/issues/6725
	// Should be removed when Gradle switch to Groovy 3
	jvmArgs('--add-opens', 'java.base/java.lang=ALL-UNNAMED')
	jvmArgs('--add-opens', 'java.base/java.lang.invoke=ALL-UNNAMED')
	jvmArgs('--add-opens', 'java.base/java.util=ALL-UNNAMED')
}
